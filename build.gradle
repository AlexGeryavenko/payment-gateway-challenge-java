plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.openapi.generator' version '7.2.0'
}

group = 'com.checkout'
version = '0.1.0'

ext {
    springBootVersion = '3.1.5'
    springdocVersion = '2.2.0'
    jsr305Version = '3.0.2'
    swaggerAnnotationsVersion = '2.2.19'
    jacksonDatabindNullableVersion = '0.2.6'
    mapstructVersion = '1.5.5.Final'
    micrometerVersion = '1.11.5'
    micrometerTracingVersion = '1.1.6'
    otelExporterVersion = '1.25.0'
    logstashEncoderVersion = '7.4'
}

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"

    implementation "com.google.code.findbugs:jsr305:${jsr305Version}"
    implementation "io.swagger.core.v3:swagger-annotations:${swaggerAnnotationsVersion}"
    implementation "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"

    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    implementation "io.micrometer:micrometer-registry-prometheus:${micrometerVersion}"
    implementation "io.micrometer:micrometer-tracing-bridge-otel:${micrometerTracingVersion}"
    implementation "io.opentelemetry:opentelemetry-exporter-otlp:${otelExporterVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashEncoderVersion}"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('generateBankClient', org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = 'java'
    inputSpec = "$projectDir/doc/openapi/bank-simulator.yaml"
    outputDir = "$buildDir/generated/bank-client"
    apiPackage = 'com.checkout.payment.gateway.client.bank.api'
    modelPackage = 'com.checkout.payment.gateway.client.bank.model'
    configOptions = [
        library             : 'resttemplate',
        useJakartaEe        : 'true',
        openApiNullable     : 'false',
        dateLibrary         : 'java8',
        serializationLibrary: 'jackson'
    ]
}

tasks.register('generatePaymentApi', org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = 'spring'
    inputSpec = "$projectDir/doc/openapi/payment-gateway.yaml"
    outputDir = "$buildDir/generated/payment-api"
    apiPackage = 'com.checkout.payment.gateway.api'
    modelPackage = 'com.checkout.payment.gateway.api.model'
    configOptions = [
        interfaceOnly       : 'true',
        useSpringBoot3      : 'true',
        useTags             : 'true',
        openApiNullable     : 'false',
        dateLibrary         : 'java8',
        skipDefaultInterface: 'true'
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/bank-client/src/main/java"
            srcDir "$buildDir/generated/payment-api/src/main/java"
        }
    }
}

compileJava.dependsOn generateBankClient, generatePaymentApi
